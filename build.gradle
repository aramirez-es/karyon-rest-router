plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.scmspain.karyon'
archivesBaseName = 'karyon-rest-router'
version = '1.3-out_interceptor-SNAPSHOT'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    maven {
        credentials {
            username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
            password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
        }

        url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
    }
    mavenCentral()
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile ('org.powermock:powermock-api-mockito:1.6.2') {
        // this mockito package includes an embedded hamcrest
        exclude group: "org.mockito", module: "mockito-all"
    }
    // mockito without hamcrest embedded
    testCompile "org.mockito:mockito-core:1.10.19"
    // hamcrest and additional matchers
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.hamcrest:hamcrest-generator:1.3'
    testCompile 'org.powermock:powermock-module-junit4:1.6.2'
    testCompile 'com.sun.jersey:jersey-core:1.19'
    testCompile 'com.netflix.karyon:karyon2-archaius:2.7.1'
    testCompile 'com.tngtech.java:junit-dataprovider:1.10.0'
    testCompile 'ch.qos.logback:logback-classic:1.1.3'


    compile 'com.netflix.karyon:karyon2-core:2.5.1'
    compile 'com.netflix.karyon:karyon2-governator:2.5.1'
    compile 'org.reflections:reflections:0.9.9'

    compile 'org.commonjava.mimeparse:mimeparse:0.1.3.3' // https://github.com/jdcasey/mimeparse
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from Javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Task publishMavenJavaPublicationToMavenRepository is added in runtime so doFirst doesn't work propertly and must be done this way
tasks.whenTaskAdded { t ->
    if( t.name == "publishMavenJavaPublicationToMavenRepository"){
        if (!project.hasProperty('artifactory_scm_user') || !project.hasProperty('artifactory_scm_password')) {
            throw new GradleException("Properties 'artifactory_scm_user' and 'artifactory_scm_password' are mandatory to do a publish, please add this information in your ~/.gradle/gradle.properties")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.scmspain'
            version project.version

            from components.java

            Artifact sourcesJar
            Artifact javadocJar

        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
                password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
            }

            url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
        }
    }
}


